#################################################################################
#
# Library: QEXT
#
# Copyright (c) 2021 ChengXueWen 1398831004@qq.com
# Copyright (c) 2014 - 2018 Axel Menzel <info@rttr.org>
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#################################################################################

#--------------------------------------------------------------------------------
# Creates a PreCompiled Header
# precompiled_headers The full path of the dependency incl. FileName
# source_files (Only works with CPP files)
#--------------------------------------------------------------------------------
function(qextFunctionActivatePrecompiledHeaders precompiled_headers source_files)
	set(SRC_FILES ${${source_files}})
  	get_filename_component(pch_basename ${precompiled_headers} NAME_WE)
  	set(pch_abs ${CMAKE_CURRENT_SOURCE_DIR}/${precompiled_headers})
  	set(pch_unity ${CMAKE_CURRENT_BINARY_DIR}/${pch_basename}.cpp)

  	if(MSVC)
    	# First specify the name of the PCH file
    	# it seems to be that nmake build cant handle the $(IntDir) variable
    	if(NOT MSVC_IDE)
      		set(pch_bin ${CMAKE_CURRENT_BINARY_DIR}/${pch_basename}.pch)
    	else()
      		set(pch_bin "$(IntDir)/${pch_basename}.pch")
    	endif()
    	# Generate precompiled header translation unit
    	if (NOT EXISTS ${pch_unity})
      		file(WRITE ${pch_unity} "// Precompiled header unity generated by CMake\n")
      		file(APPEND ${pch_unity} "#include <${pch_abs}>\n")
    	endif()
    	# this creates the precompild header
		set_source_files_properties(${pch_unity} PROPERTIES 
									COMPILE_FLAGS "/Yc\"${pch_abs}\" /Fp\"${pch_bin}\""
                               		OBJECT_OUTPUTS "${pch_bin}")
    	# Update properties of source files to use the precompiled header.
    	# Additionally, force the inclusion of the precompiled header at beginning of each source file.
		set_source_files_properties(${SRC_FILES} PROPERTIES 
									COMPILE_FLAGS "/Yu\"${pch_abs}\" /FI\"${pch_abs}\" /Fp\"${pch_bin}\""
                                	OBJECT_DEPENDS "${pch_bin}")
    	# Finally, update the source file collection to contain the precompiled header translation unit
    	set(${source_files} ${pch_unity} ${pch_abs} ${${source_files}} PARENT_SCOPE)

    	source_group("Generated Files" FILES ${pch_unity})
    	get_filename_component(PATH_TO_PCH ${precompiled_headers} DIRECTORY)
    	if (PATH_TO_PCH)
        	string ( REGEX REPLACE "[\\/]" "\\\\" normalized_path ${PATH_TO_PCH} )
        	source_group(${normalized_path} FILES ${pch_abs})
    	endif()
  	endif()
endfunction()
